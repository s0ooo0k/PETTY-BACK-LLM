plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'io.github'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

jar {
	archiveFileName = 'petty-app.jar'
	enabled = false
}

bootJar {
	archiveFileName = 'petty-app.jar'
}

repositories {
	mavenCentral()
	// SpringAI 관련 maven 설정
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
	maven {
		name = 'Central Portal Snapshots'
		url = 'https://central.sonatype.com/repository/maven-snapshots/'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'
	runtimeOnly 'com.mysql:mysql-connector-j'
	runtimeOnly 'org.postgresql:postgresql'

	// Adds MapStruct, Lombok, and lombok-mapstruct-binding for DTO mapping integration
	implementation 'org.mapstruct:mapstruct:1.6.3'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
	annotationProcessor 'org.projectlombok:lombok'

	// Adds JTS Core and Hibernate Spatial for handling spatial data types and queries
	implementation 'org.locationtech.jts:jts-core:1.20.0'
	implementation 'org.hibernate.orm:hibernate-spatial:6.6.13.Final'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Google Cloud Vision
	implementation 'com.google.cloud:google-cloud-vision:3.33.0'

	// AWS SDK v2 - Rekognition
	implementation platform('software.amazon.awssdk:bom:2.25.19')
	implementation 'software.amazon.awssdk:rekognition'

	// SMTP
	implementation 'org.springframework.boot:spring-boot-starter-mail'

	// Oauth2
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	// SpringAI API - BOM으로 버전 관리
	implementation platform("org.springframework.ai:spring-ai-bom:1.0.0-SNAPSHOT")
	implementation "org.springframework.ai:spring-ai-starter-model-openai"
	implementation "org.springframework.ai:spring-ai-starter-vector-store-qdrant"

	// gRPC 의존성 충돌 해결 - exclude로 충돌하는 버전 제외
	implementation('io.grpc:grpc-xds:1.62.2') {
		exclude group: 'io.grpc', module: 'grpc-netty-shaded'
		exclude group: 'io.grpc', module: 'grpc-core'
		exclude group: 'io.grpc', module: 'grpc-stub'
		exclude group: 'io.grpc', module: 'grpc-protobuf'
	}

	// gRPC 버전 명시적 관리
	implementation platform('io.grpc:grpc-bom:1.62.2')
	implementation 'io.grpc:grpc-netty-shaded'
	implementation 'io.grpc:grpc-protobuf'
	implementation 'io.grpc:grpc-stub'

	// Thymeleaf layout
	// https://mvnrepository.com/artifact/nz.net.ultraq.thymeleaf/thymeleaf-layout-dialect
	implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.0.0'
}

// 의존성 충돌 해결을 위한 설정
configurations.all {
	resolutionStrategy {
		// gRPC 버전 강제 통일
		force 'io.grpc:grpc-api:1.62.2'
		force 'io.grpc:grpc-core:1.62.2'
		force 'io.grpc:grpc-protobuf:1.62.2'
		force 'io.grpc:grpc-stub:1.62.2'
		force 'io.grpc:grpc-netty-shaded:1.62.2'
		force 'io.grpc:grpc-context:1.62.2'

		// Protobuf 버전 통일
		force 'com.google.protobuf:protobuf-java:3.25.3'
		force 'com.google.protobuf:protobuf-java-util:3.25.3'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}